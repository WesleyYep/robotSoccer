/*
	Fuzzy Logic for BLAZE Robot Soccer
*/

FUNCTION_BLOCK blaze	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	angle : REAL; // angle to target
	distance : REAL; // distance to target
END_VAR

VAR_OUTPUT				// Define output variable
	linear : REAL;
	angular : REAL;
END_VAR

FUZZIFY angle			// Fuzzify input variable 'angle': {'small', 'medium' , 'large'}
	TERM small := trape -10 -5 5 10 ; 
	TERM medium := (-45, 0) (-40, 1) (-15, 1) (-10, 0) (10, 0) (15, 1) (40, 1) (45, 0);
	TERM large := (-90, 1) (-40, 0) (40, 0) (90, 1);
END_FUZZIFY

FUZZIFY distance			// Fuzzify input variable 'distance': { 'veryclose', 'close' 'far' }
	TERM veryclose := (0, 1) (0.15, 0) ;
	TERM close := (0.1, 0) (0.2, 1) (0.4, 1) (0.5, 0) ;
	TERM far := (0.4, 0) (0.7, 1) ;
END_FUZZIFY

DEFUZZIFY linear			// Defzzzify output variable 'linear' : {'slow', 'medium', 'fast' }
	TERM slow := (0,1) (0.4, 0) ;
	TERM medium := (0.3, 0) (0.45, 1) (0.55, 1) (0.65, 0);
	TERM fast := (0.6, 0) (1, 1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

DEFUZZIFY angular			// Defzzzify output variable 'angular' : {'small', 'medium', 'large' }
	TERM small := (-25, 0) (-10, 1) (10, 1) (25, 0) ;
	TERM medium := (-65, 0) (-45, 1) (-30, 1) (-20, 0)(20, 0) (30, 1) (45, 1) (65, 0) ;
	TERM large := (-90, 1) (-60, 0) (60, 0) (90, 0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF angle IS small AND distance IS veryclose THEN linear IS slow;
	RULE 2 : IF angle IS small AND distance IS veryclose THEN angular IS small;
	
	RULE 3 : IF angle IS small AND distance IS close THEN linear IS medium;
	RULE 4 : IF angle IS small AND distance IS close THEN angular IS small;
	
	RULE 5 : IF angle IS small AND distance IS far THEN linear IS fast;
	RULE 6 : IF angle IS small AND distance IS far THEN angular IS small;
	
	RULE 7 : IF angle IS medium AND distance IS veryclose THEN linear IS slow;
	RULE 8 : IF angle IS medium AND distance IS veryclose THEN angular IS medium;
	
	RULE 9 : IF angle IS medium AND distance IS close THEN linear IS medium;
	RULE 10 : IF angle IS medium AND distance IS close THEN angular IS small;
	
	RULE 11 : IF angle IS medium AND distance IS far THEN linear IS fast;
	RULE 12 : IF angle IS medium AND distance IS far THEN angular IS small;
	
	RULE 13 : IF angle IS large AND distance IS veryclose THEN linear IS slow;
	RULE 14 : IF angle IS large AND distance IS veryclose THEN angular IS large;
	
	RULE 15 : IF angle IS large AND distance IS close THEN linear IS slow;
	RULE 16 : IF angle IS large AND distance IS close THEN angular IS large;
	
	RULE 17 : IF angle IS large AND distance IS far THEN linear IS medium;
	RULE 18 : IF angle IS large AND distance IS far THEN angular IS medium;
END_RULEBLOCK

END_FUNCTION_BLOCK

